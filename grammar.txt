Program → StatementList

StatementList → Statement StatementList
              | ε

Statement → OtherStatement
           | IfStatement

OtherStatement → VariableDeclaration
                | FunctionDeclaration
                | Assignment
                | LoopStatement
                | Block
                | FunctionCallStmt
                | PrintStatement
                | ReturnStatement
                | BreakStatement
                | ContinueStatement

BreakStatement → 'break' ';'

ContinueStatement → 'continue' ';'

Block → '{' StatementList '}'

VariableDeclaration → 'var' Identifier ':' Type '=' Expression ';'

Type → 'int'
      | 'float'
      | 'string'
      | 'bool'
      | 'list' '[' Type ']'
      | 'nulltype'

FunctionDeclaration → 'func' Identifier '(' ParameterList ')' ':' Type Block

ParameterList → ParameterListNonEmpty
              | ε

ParameterListNonEmpty → Parameter
                       | Parameter ',' ParameterListNonEmpty

Parameter → Identifier ':' Type

Assignment → Identifier '=' Expression ';'

IfStatement → MatchedIf
             | UnmatchedIf

MatchedIf → 'if' '(' Expression ')' Block 'else' Block

UnmatchedIf → 'if' '(' Expression ')' Block
            | 'if' '(' Expression ')' MatchedIf 'else' UnmatchedIf

LoopStatement → WhileLoop
              | ForLoop

WhileLoop → 'while' '(' Expression ')' Block

ForLoop → 'for' '(' ForAssignment ';' Expression ';' ForAssignment ')' Block

ForAssignment → Identifier '=' Expression

FunctionCallExpr → Identifier '(' ArgumentList ')'

FunctionCallStmt → FunctionCallExpr ';'

ArgumentList → ArgumentListNonEmpty
             | ε

ArgumentListNonEmpty → Expression
                      | Expression ',' ArgumentListNonEmpty

PrintStatement → 'print' '(' Expression ')' ';'

ReturnStatement → 'return' Expression ';'

Expression → SimpleExpression
           | SimpleExpression ComparativeOperator Expression

SimpleExpression → Term
                 | SimpleExpression LogicalOperator Term

Term → Factor
      | Term ArithmeticOperator Factor

Factor → Identifier
        | Literal
        | '(' Expression ')'
        | FunctionCallExpr
        | UnaryOperator Factor
        | 'NULL'

Literal → IntegerLiteral
        | FloatLiteral
        | StringLiteral
        | ListLiteral
        | 'true'
        | 'false'

ListLiteral → '[' ExpressionList ']'

ExpressionList → ExpressionListNonEmpty
               | ε

ExpressionListNonEmpty → Expression
                        | Expression ',' ExpressionListNonEmpty

ComparativeOperator → '<' | '>' | '<=' | '>=' | '==' | '!='

LogicalOperator → '&&' | '||'

ArithmeticOperator → '+' | '-' | '*' | '/' | '%'

UnaryOperator → '-' | '!'

IntegerLiteral → Digit+
FloatLiteral → Digit+ '.' Digit+
StringLiteral → '"' (Character)* '"'